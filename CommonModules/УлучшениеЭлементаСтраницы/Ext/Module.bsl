// TODO Кратинки во вкладках
// TODO Возможность подключить больше одной группы страниц


#Область ПрограммныйИнтерфейс

Функция Инициализировать(Форма, ГруппаСтраниц, Знач Родитель = Неопределено, Знач Параметры = Неопределено) Экспорт
	
	Параметры = ?(Параметры = Неопределено, Новый Структура, Параметры); 
	Родитель = ?(Родитель = Неопределено, ГруппаСтраниц.Родитель, Родитель);
	Элемент = ?(ГруппаСтраниц.Родитель = Родитель, ГруппаСтраниц, Неопределено); // элемент перед которым вставить
	
	
	ДобавитьСлужебныеРеквизитыФормы(Форма);
	ДобавитьКомандыФормы(Форма, ГруппаСтраниц);
	ЗаполнитьПараметры(Форма, Параметры);
	ДобавитьЭлементыФормы(Форма, ГруппаСтраниц, Родитель, Элемент);
	
	
	ГруппаСтраниц.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Форма.УЭС_ТекущаяСтраницаИмя = ГруппаСтраниц.ТекущаяСтраница.Имя; 
	
	
	УлучшениеЭлементаСтраницыКлиентСервер.УстановитьОформление(Форма, ГруппаСтраниц);
	
КонецФункции

Функция ПараметрыПоУмолчанию() Экспорт
	
	ПараметрыПоУмолчанию = Новый Структура;
	
	ПараметрыПоУмолчанию.Вставить("ШиринаВкладки",					16);
	ПараметрыПоУмолчанию.Вставить("Шрифт",							Новый Шрифт(, 12, Истина));
	ПараметрыПоУмолчанию.Вставить("ЦветТекстаТекущаяСтраница", 		WebЦвета.Черный);
	ПараметрыПоУмолчанию.Вставить("ЦветТекстаНеактивнаяСтраница",	WebЦвета.Серый);
	ПараметрыПоУмолчанию.Вставить("ЦветРамкиТекущаяСтраница",		WebЦвета.Черный);
	ПараметрыПоУмолчанию.Вставить("ЦветРамкиНеактивнаяСтраница",	WebЦвета.Белый);
	ПараметрыПоУмолчанию.Вставить("Выравнивание",					"Центр");
	
	Возврат ПараметрыПоУмолчанию;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьПараметры(Форма, Параметры)
	
	ПараметрыПоУмолчанию = ПараметрыПоУмолчанию();
	
	Форма.УЭС_Параметры = Новый Структура;
	
	
	Для Каждого Параметр Из ПараметрыПоУмолчанию Цикл
		
		ПараметрИмя = Параметр.Ключ;
		
		Форма.УЭС_Параметры.Вставить(
				ПараметрИмя,
				?(Параметры.Свойство(ПараметрИмя),
						Параметры[ПараметрИмя],
						ПараметрыПоУмолчанию[ПараметрИмя]
					)
			);
		
	КонецЦикла;
	
КонецФункции

Функция ДобавитьСлужебныеРеквизитыФормы(Форма)
	
	ОписаниеТиповСтрока100 = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100));
	
	
	МассивНовыхРеквизитов = Новый Массив;
	
	МассивНовыхРеквизитов.Добавить(Новый РеквизитФормы("УЭС_Параметры", Новый ОписаниеТипов));
	МассивНовыхРеквизитов.Добавить(Новый РеквизитФормы("УЭС_ТекущаяСтраницаИмя", ОписаниеТиповСтрока100));
	
	
	Форма.ИзменитьРеквизиты(МассивНовыхРеквизитов);
	
КонецФункции

Функция ДобавитьКомандыФормы(Форма, ГруппаСтраниц)
	
	Для Каждого СтраницаГруппы Из ГруппаСтраниц.ПодчиненныеЭлементы Цикл
		
		НаименованиеКоманды = УлучшениеЭлементаСтраницыКлиентСервер.ПолучитьИмяКомандыДляСтраницы(СтраницаГруппы.Имя);
		
		НоваяКоманда = Форма.Команды.Добавить(НаименованиеКоманды);
		НоваяКоманда.Заголовок = СтраницаГруппы.Заголовок;
		НоваяКоманда.Действие = СтрШаблон("Подключаемый_%1ПриСменеСтраницы", ГруппаСтраниц.Имя);
		
	КонецЦикла;
	
КонецФункции

Функция ДобавитьЭлементыФормы(Форма, ГруппаСтраниц, Родитель, Элемент)
	
	НаименованиеГруппы = СтрШаблон("УЭС_%1Вкладки", ГруппаСтраниц.Имя);
	
	ГруппаВкладок = Форма.Элементы.Вставить(НаименованиеГруппы, Тип("ГруппаФормы"), Родитель, Элемент); 
	ГруппаВкладок.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаВкладок.РастягиватьПоГоризонтали = Истина;
	ГруппаВкладок.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаВкладок.Отображение = ОтображениеОбычнойГруппы.Нет; 
	ГруппаВкладок.ОтображатьЗаголовок = Ложь;
	
	ГруппаВкладок.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента[Форма.УЭС_Параметры.Выравнивание];
	
	Для Каждого СтраницаГруппа Из ГруппаСтраниц.ПодчиненныеЭлементы Цикл
		
		НаименованиеГруппыВкладка = СтрШаблон("УЭС_%1Вкладка%2", ГруппаСтраниц.Имя, СтраницаГруппа.Имя);
		
		ГруппаВкладка = Форма.Элементы.Вставить(НаименованиеГруппыВкладка, Тип("ГруппаФормы"), ГруппаВкладок);
		ГруппаВкладка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаВкладка.РастягиватьПоГоризонтали = Ложь;
		
		ГруппаВкладка.Ширина = Форма.УЭС_Параметры.ШиринаВкладки;
		
		ГруппаВкладка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		
		
		ДобавитьКомандуВГруппуВкладка(Форма, ГруппаВкладка, СтраницаГруппа);
		
	КонецЦикла;
	
КонецФункции             

Функция ДобавитьКомандуВГруппуВкладка(Форма, ГруппаВкладка, СтраницаГруппа)
	
	НаименованиеКнопки = УлучшениеЭлементаСтраницыКлиентСервер.ПолучитьИмяКнопкиДляСтраницы(СтраницаГруппа.Имя);
	
	ЭлементКнопка = Форма.Элементы.Добавить(НаименованиеКнопки, Тип("КнопкаФормы"), ГруппаВкладка);
	ЭлементКнопка.ИмяКоманды = УлучшениеЭлементаСтраницыКлиентСервер.ПолучитьИмяКомандыДляСтраницы(СтраницаГруппа.Имя);
	ЭлементКнопка.ОтображениеФигуры = ОтображениеФигурыКнопки.Нет;
	
	ЭлементКнопка.Шрифт = Форма.УЭС_Параметры.Шрифт;   
	
	
	НаименованиеДекорации = УлучшениеЭлементаСтраницыКлиентСервер.ПолучитьИмяДекорацииДляСтраницы(СтраницаГруппа.Имя);
	
	ЭлементДекорация = Форма.Элементы.Добавить(НаименованиеДекорации, Тип("ДекорацияФормы"), ГруппаВкладка);
	ЭлементДекорация.Шрифт = Новый Шрифт(, 1);
	ЭлементДекорация.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Подчеркивание, 2);
	ЭлементДекорация.РастягиватьПоГоризонтали = Ложь;
	
	ЭлементДекорация.Ширина = Форма.УЭС_Параметры.ШиринаВкладки;
	
КонецФункции

#КонецОбласти