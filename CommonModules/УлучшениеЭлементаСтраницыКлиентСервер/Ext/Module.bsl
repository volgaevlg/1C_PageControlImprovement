#Область ПрограммныйИнтерфейс

Процедура ПереключитьСтраницу(Форма, КомандаИмя) Экспорт
	
	НоваяСтраница = ПолучитьСтраницуПоКоманде(Форма, КомандаИмя);
	
	ГруппаСтраниц = НоваяСтраница.Родитель;
	
	Если ГруппаСтраниц.ТекущаяСтраница = НоваяСтраница Тогда
		Возврат;
	КонецЕсли;
	
	
	ГруппаСтраниц.ТекущаяСтраница = НоваяСтраница;
	УстановитьОформление(Форма, ГруппаСтраниц);
	
КонецПроцедуры

Процедура УстановитьОформление(Форма, ГруппаСтраниц) Экспорт
	
	Для Каждого Страница Из ГруппаСтраниц.ПодчиненныеЭлементы Цикл
		
		ЭтоТекущаяСтраница = (ГруппаСтраниц.ТекущаяСтраница = Страница);
		
		ЭлементКнопка		= Форма.Элементы[ПолучитьИмяКнопкиДляСтраницы(Страница.Имя)];
		ЭлементДекорация	= Форма.Элементы[ПолучитьИмяДекорацииДляСтраницы(Страница.Имя)];
		
		Если ЭтоТекущаяСтраница Тогда
			ЭлементКнопка.ЦветТекста = Форма.УЭС_Параметры.ЦветТекстаТекущаяСтраница;
			ЭлементДекорация.ЦветРамки = Форма.УЭС_Параметры.ЦветРамкиТекущаяСтраница; 
		Иначе
			ЭлементКнопка.ЦветТекста = Форма.УЭС_Параметры.ЦветТекстаНеактивнаяСтраница;
			ЭлементДекорация.ЦветРамки = Форма.УЭС_Параметры.ЦветРамкиНеактивнаяСтраница; 
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейсСлужебный

Функция ПолучитьСтраницуПоКоманде(Форма, КомандаИмя) Экспорт
	
	ИмяСтраницы = СтрЗаменить(КомандаИмя, ПолучитьИмяКомандыДляСтраницы(), "");
	
	Возврат Форма.Элементы[ИмяСтраницы];
	
КонецФункции

Функция ПолучитьИмяКомандыДляСтраницы(СтраницаГруппаИмя = "") Экспорт
	
	Возврат СтрШаблон("УЭС_Вкладка%1", СтраницаГруппаИмя);
	
КонецФункции

Функция ПолучитьИмяКнопкиДляСтраницы(СтраницаГруппаИмя = "") Экспорт
	
	Возврат СтрШаблон("УЭС_КнопкаВкладка%1", СтраницаГруппаИмя);
	
КонецФункции

Функция ПолучитьИмяДекорацииДляСтраницы(СтраницаГруппаИмя = "") Экспорт
	
	Возврат СтрШаблон("УЭС_ДекорацияВкладка%1", СтраницаГруппаИмя);
	
КонецФункции

#КонецОбласти